import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'net.nemerosa.versioning' version '2.6.1'
    id 'edu.sc.seis.launch4j' version '2.4.6'
    id "org.beryx.runtime" version "1.8.4"
}

group 'co.aplicared.jvm.juego.arbucies'
version '0.01'
compileJava.sourceCompatibility = compileJava.targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

application {
    mainClassName = 'co.aplicared.jvm.juego.arbucies.ArbuciesKt'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    // JAXB
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.4.0-b180830.0438'

    // JAX-WS
    implementation 'com.sun.xml.ws:jaxws-ri:2.3.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
        publishAlways()
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes (
                "Implementation-Title": project.name,
                'Main-Class' : 'co.aplicared.jvm.juego.arbucies.ArbuciesKt',
                'Built-By' : System.properties['user.name'],
                'Build-Revision' : versioning.info.commit,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}
launch4j {
    mainClassName = 'co.aplicared.jvm.juego.arbucies.ArbuciesKt'
    //icon = "${projectDir}/icons/myApp.ico"
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

tasks.runtime.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/image/bin")
    }
}